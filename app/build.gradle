plugins {
    alias libs.plugins.kotlin
    alias libs.plugins.application
    alias libs.plugins.junit5
    alias libs.plugins.hilt
    alias libs.plugins.play
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    def PACKAGE_NAME = "com.w2sv.autocrop"

    compileSdk libs.versions.compileSdk.get().toInteger()
    namespace PACKAGE_NAME

    defaultConfig {
        applicationId PACKAGE_NAME

        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.compileSdk.get().toInteger()

        versionCode project.findProperty('versionCode').toInteger()
        versionName version

        buildConfigField "int", "MIN_SDK_VERSION", "$minSdkVersion.apiLevel"
        setProperty('archivesBaseName', versionName)
        // Stores built aab's as "{versionName}-{buildFlavor}.apk"

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                arguments "-DOpenCV_DIR=${rootProject.projectDir.toString()}/opencv/native"
            }
        }

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }
    signingConfigs {
        release {
            // create release signingConfig from keystore.properties file, if present
            rootProject.file("keystore.properties").with { file ->
                if (file.exists()) {
                    def keystoreProperties = new Properties()
                    keystoreProperties.load(new FileInputStream(file))

                    storeFile rootProject.file("keys.jks")
                    storePassword keystoreProperties["storePassword"]
                    keyAlias keystoreProperties["keyAlias"]
                    keyPassword keystoreProperties["keyPassword"]
                } else
                    println("WARNING: Failed to create release signing configuration; $file.path not present")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            debuggable true
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }
    testOptions {
        animationsDisabled = true
    }
    buildFeatures {
        viewBinding true
    }
    // Store built apk's as "{versionName}.apk"
    applicationVariants.configureEach { variant ->
        variant.outputs.all {
            outputFileName = "${variant.versionName}.apk"
        }
    }
}
// https://github.com/Triple-T/gradle-play-publisher
play {
    serviceAccountCredentials.set(file("../service-account-key.json"))
    defaultToAppBundles.set(true)
    track.set("production")
}
dependencies {
    // androidx
    implementation libs.androidx.appcompat
    implementation libs.androidx.core
    implementation libs.androidx.viewmodel
    implementation libs.androidx.viewpager2
    implementation libs.androidx.fragment
    implementation libs.androidx.splashscreen

    // google
    implementation libs.google.material
    implementation libs.google.hilt
    kapt libs.google.hilt.kapt

    // owned
    implementation project(path: ':cropbundle')
    implementation project(path: ':screenshotlistening')
    implementation project(path: ':common')
    implementation project(path: ':flowfield')
    implementation project(path: ':opencv')

    implementation libs.kotlinutils
    implementation libs.viewboundcontroller
    implementation libs.androidutils
    implementation libs.viewanimations
    implementation libs.onboarder
    implementation libs.bidirectionalviewpager

    // other
    implementation libs.slimber
    implementation libs.animatoo
    implementation libs.lottie

    // ---------------
    // unitTest
    testImplementation libs.bundles.unitTest

    // ---------------
    // androidTest
    androidTestImplementation libs.bundles.androidTest
    androidTestRuntimeOnly libs.junit5.mannodermaus.runner

    // espresso
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.espresso.intents
}