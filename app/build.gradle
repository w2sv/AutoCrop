plugins{
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'de.mannodermaus.android-junit5'
}

// import @loadedProperties from ./utils.gradle
apply from: "$rootDir/utils.gradle"

android {
    def MIN_SDK_VERSION = 26
    def COMPILE_SDK_VERSION = 33

    def PACKAGE_NAME = 'com.w2sv.autocrop'

    compileSdkVersion COMPILE_SDK_VERSION
    namespace PACKAGE_NAME

    defaultConfig {
        applicationId PACKAGE_NAME

        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion COMPILE_SDK_VERSION
        buildConfigField "int", "MIN_SDK_VERSION", "$minSdkVersion.apiLevel"

        versionCode 26
        versionName '3.2.1'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }
    signingConfigs {
        // create release signingConfig from keystore.properties file, if present
        release {
            rootProject.file("keystore.properties").with{propertyFile ->
                if (propertyFile.exists()){
                    storeFile rootProject.file("keys.jks")

                    def keystoreProperties = loadedProperties(propertyFile)
                    storePassword keystoreProperties["storePassword"]
                    keyAlias keystoreProperties["keyAlias"]
                    keyPassword keystoreProperties["keyPassword"]
                }
                else
                    println("WARNING: Failed to create release signing configuration; $propertyFile.path not present")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            debuggable false  // set to true to get logs whilst in release mode
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    testOptions {
        animationsDisabled = true
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")  // fixes build error
    }
    buildFeatures {
        viewBinding true
    }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Processing
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    // Modules
    implementation project(path: ':croppylib')
    implementation project(path: ':bidirectionalviewpager')

    // Androidx
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.3'  // enable @lifecycleScope
    implementation 'androidx.activity:activity-ktx:1.6.0'  // enable @lifecycleScope
    implementation 'androidx.core:core-splashscreen:1.0.0'

    // Data structures
    implementation 'com.google.guava:guava:31.1-jre'  // EvictingQueue, Sets.difference

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Snackbar
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.github.matecode:Snacky:1.1.5'

    // Animations

    // .Activity Transitions
    implementation 'com.github.mohammadatif:Animatoo:master'
    // .View Animations
    implementation 'com.daimajia.androidanimations:library:2.4@aar'
    implementation 'com.daimajia.easing:library:2.4@aar'

    // -----------------
    // Unit Tests
    // -----------------

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'androidx.test.ext:junit:1.1.3'

    // -----------------
    // Instrumented Tests
    // -----------------

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.3.0'
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.3.0'

    // Jupiter Params
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
}

