plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'de.mannodermaus.android-junit5'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    namespace rootProject.ext.packageName

    defaultConfig {
        applicationId rootProject.ext.packageName

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        buildConfigField "int", "MIN_SDK_VERSION", "$minSdkVersion.apiLevel"

        versionCode 30
        versionName '3.2.5'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }
    signingConfigs {
        release {
            // create release signingConfig from keystore.properties file, if present
            rootProject.file("keystore.properties").with { file ->
                if (file.exists()) {
                    def keystoreProperties = new Properties()
                    keystoreProperties.load(new FileInputStream(file))

                    storeFile rootProject.file("keys.jks")
                    storePassword keystoreProperties["storePassword"]
                    keyAlias keystoreProperties["keyAlias"]
                    keyPassword keystoreProperties["keyPassword"]
                } else
                    println("WARNING: Failed to create release signing configuration; $file.path not present")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    testOptions {
        animationsDisabled = true
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")  // fixes build error
    }
    buildFeatures {
        viewBinding true
    }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Processing
    implementation files('libs/processing-core-4.3.0.jar')

    // Modules
    implementation project(path: ':bidirectionalviewpager')
    implementation project(path: ':permissionhandler')
    implementation 'com.github.w2sv:KotlinUtils:0.0.3'
    implementation 'com.github.w2sv:AndroidUtils:0.0.3'
    implementation 'com.github.w2sv:ViewboundController:0.0.6'
    implementation 'com.github.w2sv:TypedPreferences:1.0.0'

    // Androidx
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.4'  // enable @lifecycleScope
    implementation 'androidx.activity:activity-ktx:1.6.1'  // enable @lifecycleScope
    implementation 'androidx.core:core-splashscreen:1.0.0'

    // Data structures
    implementation 'com.google.guava:guava:31.1-jre'  // EvictingQueue, Sets.difference

    // Logging
    implementation 'com.github.PaulWoitaschek:Slimber:2.0.0'

    // Snackbar
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.github.matecode:Snacky:1.1.5'

    // Animations
    implementation 'com.github.mohammadatif:Animatoo:master'
    implementation 'com.daimajia.androidanimations:library:2.4@aar'
    implementation 'com.daimajia.easing:library:2.4@aar'

    implementation 'com.google.dagger:hilt-android:2.42'
    kapt 'com.google.dagger:hilt-android-compiler:2.42'

    // -----------------
    // Unit Tests
    // -----------------

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'androidx.test.ext:junit:1.1.4'

    // -----------------
    // Instrumented Tests
    // -----------------

    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.3.0'
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.3.0'

    // Jupiter Params
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.0'
}

